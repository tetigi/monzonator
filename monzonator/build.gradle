apply plugin: 'kotlin'
apply plugin: 'com.palantir.sls-java-service-distribution'

dependencies {
    compile project(':monzonator-api')
    compile 'com.palantir.remoting3:retrofit2-clients'
    compile 'com.palantir.remoting3:jaxrs-clients'
    compile 'io.dropwizard:dropwizard-core'

    testCompile 'junit:junit'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


distribution {
    serviceName 'monzonator'
    mainClass 'com.tetigi.monzonator.Main'
}
buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}

def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "tetigi"
			name "Luke Tomlin"
			email "luke.tomlin@gmail.com"
		}
	}

	scm {
		url "https://github.com/tetigi/monzonator"
	}
}

bintray {
	user = System.getProperty('bintray.user')
	key = System.getProperty('bintray.key')
	publications = ['mavenPublication']

	pkg {
		repo = 'Monzonator'
		name = 'monzonator'
		userOrg = 'tetigi'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/tetigi/monzonator.git'
		version {
			name = gitVersion()
			desc = gitVersion()
			released  = new Date()
		}
	}

}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.tetigi'
            artifactId 'monzonator'
            version gitVersion()
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An it-just-works Kotlin lib for Monzo.')
                root.appendNode('name', 'monzonator')
                root.appendNode('url', 'https://github.com/tetigi/monzonator')
                root.children().last() + pomConfig
            }
        }
    }
}
